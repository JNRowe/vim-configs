#! /bin/zsh

# Iterate over dein managed repositories looking for modifications.
#
# The primary use of this script is to hunt out quick hacks that have been made
# to repositories that may have been forgotten about.

zparseopts -D -K -E -A args s h v::

if [[ -n ${args[(I)-h]} ]] {
    print -P "Usage: %B$0%b [optionâ€¦]"
    echo "Options:"
    echo "    -s         Display the repository name only"
    echo "    -v [name]  Show changes, optionally followed by a git diff option"
    echo "    -h         This message"
    exit 0
}

for d (${XDG_CACHE_HOME:-~/.cache}/vim/dein/repos/github.com/*/*(/)) {
    cd $d
    if git status -s | grep -q '^.M'; then
        if [[ -n ${args[(I)-s]} ]] {
            echo ${d:h:t}/${d:t}
        } else {
            print -D $d
        }
        if [[ -n ${args[(I)-v]} ]] {
            git diff ${args[(I)-v]+--${args[-v]}}
        }
    fi
}
